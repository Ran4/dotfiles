#vim: ft=zsh
function +vi-set_novcs_prompt_symbol {
  _prompt_giddie_symbol=')'
}

function +vi-set_vcs_prompt_symbol {
  _prompt_giddie_symbol='Â±'
}

function +vi-git_precmd {
  # Check for untracked files, since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    hook_com[unstaged]+='%F{red}?%f'
  fi
}

function prompt_giddie_precmd {
  # Replace '/home/<user>' with '~'.
  _prompt_giddie_pwd="${PWD/#$HOME/~}"
  vcs_info
}

function prompt_giddie_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent sp subst)

  # Load required functions.
  autoload -Uz vcs_info
  autoload -Uz add-zsh-hook

  # Add hook to set up prompt parameters before each command.
  add-zsh-hook precmd prompt_giddie_precmd

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%F{green}...%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format '%F{yellow}--- COMMAND ---%f'

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' check-for-changes true
  zstyle ':vcs_info:*' formats ' %F{13}%b%f%c%u'
  
  zstyle ':vcs_info:*' actionformats ' on %F{magenta}%b%f%c%u %F{yellow}(%a)%f'
  zstyle ':vcs_info:*' stagedstr ' %F{green}D%f'
  zstyle ':vcs_info:*' unstagedstr ' %F{red}M%f'

  # Set vcs_info hooks.
  # NOTE: Prior to Zsh v4.3.12, there are no static hooks, no vcs_info_hookadd
  #       function, and no 'no-vcs' hook.
  zstyle ':vcs_info:*+start-up:*' hooks set_novcs_prompt_symbol
  zstyle ':vcs_info:git*+set-message:*' hooks set_vcs_prompt_symbol git_precmd
  zstyle ':vcs_info:*+set-message:*' hooks set_vcs_prompt_symbol

  # Define prompts.
  # PROMPT='%F{35}${_prompt_giddie_pwd}%f${vcs_info_msg_0_}%F{yellow} $%f '
  PROMPT='%F{35}${_prompt_giddie_pwd}%f${vcs_info_msg_0_}%B%F{yellow} $%f%b '
  RPROMPT='${editor_info[keymap]}'
  SPROMPT='zsh: correct %F{magenta}%R%f to %F{green}%r%f [nyae]? '
}

prompt_giddie_setup "$@"
